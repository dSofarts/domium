plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

group = 'ru.domium'
version = "$appVersion"
description = 'Domium Common Security'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    enabled = true
}

tasks.named('bootJar') {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version
            
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

tasks.named('generateMetadataFileForMavenPublication') {
    suppressedValidationErrors.add('dependencies-without-versions')
}

