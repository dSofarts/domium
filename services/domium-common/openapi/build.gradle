plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

group = 'ru.domium'
version = "$appVersion"
description = 'Domium Common OpenAPI'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    api "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0"

    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    compileOnly 'org.springframework:spring-webmvc'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    enabled = true
    archiveClassifier = ''
}

tasks.named('bootJar') {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

tasks.named('generateMetadataFileForMavenPublication') {
    suppressedValidationErrors.add('dependencies-without-versions')
}
