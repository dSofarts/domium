plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

group = 'ru.domium'
version = "$appVersion"
description = 'Building API (DTO)'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api "io.swagger.core.v3:swagger-annotations-jakarta:$swaggerAnnotationsVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    enabled = true
}

tasks.named('bootJar') {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'building-api'
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

tasks.named('generateMetadataFileForMavenPublication') {
    suppressedValidationErrors.add('dependencies-without-versions')
}
